grammar org.openhab.model.Sitemap with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate sitemap "http://www.openhab.org/model/Sitemap"

SitemapModel :
	'sitemap' Sitemap;

Sitemap :
		name=ID ('label=' label=STRING)? ('icon=' icon=STRING)? '{'
			(children+=Widget)+
		'}';

Widget :
	(LinkableWidget | NonLinkableWidget);	

NonLinkableWidget :
	Switch | Selection | Slider | List | Setpoint | Video | Chart | Webview | Colorpicker
;

LinkableWidget :
	(Text | Group | Image | Frame)
	('{'
			(children+=Widget)+
	'}')?;

Frame : {Frame} 'Frame' (('item=' item=ItemRef)? & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)?);

Text : 
	{Text} 'Text' (('item=' item=ItemRef)? & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('range=[' range+=Range (',' range+=Range)* ']')?);
	
Group : 
	'Group' (('item=' item=GroupItemRef) & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)?);

Image : 
	'Image' (('item=' item=ItemRef)? & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('url=' url=(STRING)) ('refresh=' refresh=INT)?);

Video : 
	'Video' (('item=' item=ItemRef)? & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('url=' url=(STRING)));

Chart : 
	'Chart' (('item=' item=ItemRef) & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('service=' service=(STRING))? & ('refresh=' refresh=INT)? & ('period=' period=ID));

Webview : 
	'Webview' (('item=' item=ItemRef)? & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('url=' url=(STRING)) & ('height=' height=INT)?);
	
Switch : 
	'Switch' (('item=' item=ItemRef) & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('mappings=[' mappings+=Mapping (',' mappings+=Mapping)* ']')?);
	
Slider :
	'Slider' (('item=' item=ItemRef) & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('sendFrequency=' frequency=INT)? & (switchEnabled?='switchSupport')?);
	
Selection : 
	'Selection' (('item=' item=ItemRef) & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('mappings=[' mappings+=Mapping (',' mappings+=Mapping)* ']')?);

List : 
	'List' (('item=' item=ItemRef) & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? ('separator=' separator=STRING));

Setpoint : 
	'Setpoint' (('item=' item=ItemRef) & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('minValue=' minValue=Number)? & ('maxValue=' maxValue=Number)? & ('step=' step=Number)?);

Colorpicker : 
	'Colorpicker' (('item=' item=ItemRef) & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('sendFrequency=' frequency=INT)?);

Range :
	val=RangeVal ':' icon=(STRING) ':' color=(STRING);

RangeVal returns ecore::EString:
	INT | FLOAT | STRING;


Mapping :
	cmd=Command '=' label=(ID|STRING);

ItemRef :
	ID
;

GroupItemRef :
	ID
;

Icon :
	STRING
;

Command returns ecore::EString:
	INT | ID | STRING;

Number returns ecore::EBigDecimal:
	INT | FLOAT;


terminal ID : ('^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*) |
	 (('0'..'9')+ ('a'..'z'|'A'..'Z'|'_') ('0'..'9'|'a'..'z'|'A'..'Z'|'_')*);
	
terminal FLOAT returns ecore::EBigDecimal:
	INT '.' INT;
