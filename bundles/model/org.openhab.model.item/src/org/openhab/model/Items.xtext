grammar org.openhab.model.Items with org.eclipse.xtext.common.Terminals

generate items "http://www.openhab.org/model/Items"

ItemModel :
	{ItemModel} (items+=ModelItem)*
;

ModelItem :
	(ModelNormalItem | ModelGroupItem) name=ID
	(label=STRING)?
	('<' icon=(ID|STRING) '>')?
	('(' groups+=ID (',' groups+=ID)* ')')? 
	('{' bindings+=ModelBinding (',' bindings+=ModelBinding)* '}')? 
;

ModelGroupItem :
	{ModelGroupItem} 'Group' (':' type=ModelItemType ( ':' function=ModelGroupFunction ('(' args+=(ID|STRING) (',' args+=(ID|STRING))* ')')?)?)?
;

enum ModelGroupFunction :
	AND='AND' | OR='OR' | NAND='NAND' | NOR='NOR' | AVG='AVG' | SUM='SUM' | MAX='MAX' | MIN='MIN' | COUNT='COUNT'
;

ModelNormalItem :
	type=ModelItemType
;

ModelItemType :
	'Switch' | 'Rollershutter' | 'Number' | 'String' | 'Dimmer' | 'Contact' | 'DateTime' | 'Color' | 'Location' | ID
;

ModelBinding:
	type=ID '=' configuration=STRING
;
